<?xml version='1.0'?>
<effect language="hlsl">
    <include name="sys:fx/Gbuffer.fxml"/>
    <include name="sys:fx/inputlayout.fxml"/>
    <parameter name="matModelViewProject" type="float4x4" semantic="matModelViewProject" />
    <parameter name="matModelViewInverse" type="float4x4" semantic="matModelViewInverse"/>
    <parameter name="quality" type="float4"/>
    <parameter name="diffuse" type="float3"/>
    <parameter name="metalness" type="float"/>
    <parameter name="smoothness" type="float"/>
    <parameter name="texDiffuse" type="texture2D"/>
    <parameter name="texNormal" type="texture2D" />
    <parameter name="texSpecular" type="texture2D" />
    <buffer name="joints">
        <parameter name="bindpose[NUM_JOINT]" type="float4x4[]"/>
    </buffer>
    <shader>
        <![CDATA[
            float3 DecodeNormalMap(Texture2D normal, float2 coord)
            {
                float3 bump;
                bump.xy = normal.Sample(PointClamp, coord).gr * 2.0 - 1.0;
                bump.z = sqrt(1.0 - bump.x * bump.x - bump.y * bump.y);
                return normalize(bump);
            }

            float3 GetDiffuse(Texture2D diffuseMap, float3 diffuse, float2 coord)
            {
                return srgb2linear(diffuseMap.Sample(PointClamp, coord).rgb) * diffuse;
            }

            float3 GetNormal(Texture2D normalMap, float3 normal, float3 tangent, float2 coord, float quality)
            {
                float3 tangentNormal;
                if (quality > 0.55)
                    tangentNormal = normalMap.Sample(PointClamp, coord).rgb * 2 - 1;
                else
                    tangentNormal = DecodeNormalMap(normalMap, coord);

                float3 N = normalize(normal);
                float3 T = normalize(tangent);
                float3 B = cross(N, T);

                float3x3 tbn = float3x3(T, B, N);
                float3 highNormal = mul(tangentNormal, tbn);
                return normalize(highNormal);
            }

            float2 GetSpecular(Texture2D specularMap, float2 coord)
            {
                return specularMap.Sample(PointClamp, coord).rg;
            }

            void DepthVS(
                in float4 Position : POSITION,
                in float4 BlendWeight : BLENDWEIGHT,
                in float4 BlendIndice : BLENDINDICES,
                out float4 oPosition : SV_Position)
            {
                int4 blendIndices = (int4)BlendIndice;

                oPosition = mul(bindpose[blendIndices.x], Position) * BlendWeight.x;
                oPosition += mul(bindpose[blendIndices.y], Position) * BlendWeight.y;
                oPosition += mul(bindpose[blendIndices.z], Position) * BlendWeight.z;
                oPosition += mul(bindpose[blendIndices.w], Position) * BlendWeight.w;
                oPosition = mul(matModelViewProject, oPosition);
            }

            void DepthPS()
            {
            }

            void OpaqueVS(
                in float4 Position : POSITION,
                in float4 TangentQuat : TANGENT,
                in float2 Texcoord : TEXCOORD,
                in float4 BlendWeight : BLENDWEIGHT,
                in float4 BlendIndice : BLENDINDICES,
                out float3 oNormal : TEXCOORD0,
                out float3 oTangent : TEXCOORD1,
                out float2 oTexcoord : TEXCOORD2,
                out float4 oPosition : SV_Position)
            {
                TangentQuat = TangentQuat * 2 - 1;

                float3 Normal = QuaternionToNormal(TangentQuat);
                float3 Tangent = QuaternionToTangent(TangentQuat);

                int4 blendIndices = (int4)BlendIndice;

                oPosition = mul(bindpose[blendIndices.x], Position) * BlendWeight.x;
                oPosition += mul(bindpose[blendIndices.y], Position) * BlendWeight.y;
                oPosition += mul(bindpose[blendIndices.z], Position) * BlendWeight.z;
                oPosition += mul(bindpose[blendIndices.w], Position) * BlendWeight.w;

                oNormal = mul((float3x3)bindpose[blendIndices.x], Normal) * BlendWeight.x;
                oNormal += mul((float3x3)bindpose[blendIndices.y], Normal) * BlendWeight.y;
                oNormal += mul((float3x3)bindpose[blendIndices.z], Normal) * BlendWeight.z;
                oNormal += mul((float3x3)bindpose[blendIndices.w], Normal) * BlendWeight.w;

                oNormal = mul(oNormal, (float3x3)matModelViewInverse);

                oTexcoord = Texcoord;
                oPosition = mul(matModelViewProject, oPosition);
            }

            GbufferParam OpaquePS(in float3 iNormal : TEXCOORD0, in float3 iTangent : TEXCOORD1, in float2 iTexcoord : TEXCOORD2)
            {
                MaterialParam material;
                material.albedo = quality.r > 0.0 ? GetDiffuse(texDiffuse, diffuse, iTexcoord) : diffuse.rgb;
                material.normal = quality.g > 0.0 ? GetNormal(texNormal, iNormal, iTangent, iTexcoord, quality.g) : normalize(iNormal);

                if (quality.b > 0.0)
                {
                    float4 specularTex = texSpecular.Sample(PointClamp, iTexcoord);
                    material.specular = specularTex.r;
                    material.smoothness = specularTex.g;
                }
                else
                {
                    material.specular = metalness;
                    material.smoothness = smoothness;
                }

                float3 dielectricColor = float3(0.04, 0.04, 0.04);
                float3 diffColor = material.albedo * (1.0 - material.specular);
                float3 specColor = lerp(dielectricColor.rgb, material.albedo, material.specular);

                material.albedo = diffColor;
                material.specular = specColor;
                material.transmittance = 0;
                material.occlusion = 1;
                material.lightModel = LIGHTINGMODEL_NORMAL;

                return EncodeGbuffer(material);
            }
        ]]>
    </shader>
    <technique name="Shadow">
        <pass name="p0">
            <state name="inputlayout" value="POS3F_T4F_UV2F_W4F_B4UI"/>
            <state name="vertex" value="DepthVS"/>
            <state name="fragment" value="DepthPS"/>
            <state name="primitive" value="triangle" />
            <state name="stencilTest" value="true"/>
            <state name="stencilPass" value="replace"/>
            <state name="stencilTwoPass" value="replace"/>
        </pass>
    </technique>
    <technique name="Opaque">
        <pass name="p0">
            <state name="inputlayout" value="POS3F_T4F_UV2F_W4F_B4UI"/>
            <state name="vertex" value="OpaqueVS"/>
            <state name="fragment" value="OpaquePS"/>
            <state name="primitive" value="triangle" />
            <state name="colormask0" value="rgba" />
            <state name="colormask1" value="rgba" />
            <state name="stencilTest" value="true"/>
            <state name="stencilPass" value="replace"/>
            <state name="stencilTwoPass" value="replace"/>
        </pass>
    </technique>
</effect>