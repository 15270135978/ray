<?xml version='1.0'?>
<effect language="hlsl">
    <include name="sys:fx/Gbuffer.fxml"/>
    <include name="sys:fx/inputlayout.fxml"/>
    <parameter name="matModelViewProject" type="float4x4" semantic="matModelViewProject" />
    <parameter name="matView" type="float4x4" semantic="matView"/>
    <parameter name="quality" type="float4"/>
    <parameter name="diffuse" type="float3"/>
    <parameter name="specular" type="float"/>
    <parameter name="shininess" type="float"/>
    <parameter name="texDiffuse" type="texture2D"/>
    <parameter name="texSpecular" type="texture2D" />
    <parameter name="texNormal" type="texture2D" />
    <buffer name="joints">
        <parameter name="bindpose[NUM_JOINT]" type="float4x4[]"/>
    </buffer>
    <shader>
        <![CDATA[
            float3 SphereNormalMap(Texture2D normal, float2 coord)
            {
                float3 bump;
                bump.xy = normal.Sample(PointClamp, coord).gr * 2.0 - 1.0;
                bump.z = sqrt(1.0 - bump.x * bump.x - bump.y * bump.y);
                return normalize(bump);
            }

            float3 GetNormal(Texture2D normal, float2 coord, float quality)
            {
                if (quality > 0.5)
                    return SphereNormalMap(normal, coord);
                else
                    return normal.Sample(PointClamp, coord).rgb;
            }

            void DepthVS(
                in float4 Position : POSITION,
                in float4 BlendWeight : BLENDWEIGHT,
                in float4 BlendIndice : BLENDINDICES,
                out float4 oPosition : SV_Position)
            {
                oPosition = mul(bindpose[(uint)BlendIndice.x], Position) * BlendWeight.x;
                oPosition += mul(bindpose[(uint)BlendIndice.y], Position) * BlendWeight.y;
                oPosition += mul(bindpose[(uint)BlendIndice.z], Position) * BlendWeight.z;
                oPosition += mul(bindpose[(uint)BlendIndice.w], Position) * BlendWeight.w;
                oPosition = mul(matModelViewProject, oPosition);
            }

            void DepthPS()
            {
            }

            void OpaqueVS(
                in float4 Position : POSITION,
                in float3 Normal : NORMAL,
                in float2 Texcoord : TEXCOORD,
                in float4 BlendWeight : BLENDWEIGHT,
                in float4 BlendIndice : BLENDINDICES,
                out float3 oNormal : TEXCOORD0,
                out float2 oTexcoord : TEXCOORD1,
                out float4 oPosition : SV_Position)
            {
                oPosition = mul(bindpose[(uint)BlendIndice.x], Position) * BlendWeight.x;
                oPosition += mul(bindpose[(uint)BlendIndice.y], Position) * BlendWeight.y;
                oPosition += mul(bindpose[(uint)BlendIndice.z], Position) * BlendWeight.z;
                oPosition += mul(bindpose[(uint)BlendIndice.w], Position) * BlendWeight.w;

                oNormal = mul((float3x3)bindpose[(uint)BlendIndice.x], Normal) * BlendWeight.x;
                oNormal += mul((float3x3)bindpose[(uint)BlendIndice.y], Normal) * BlendWeight.y;
                oNormal += mul((float3x3)bindpose[(uint)BlendIndice.z], Normal) * BlendWeight.z;
                oNormal += mul((float3x3)bindpose[(uint)BlendIndice.w], Normal) * BlendWeight.w;
                oNormal = mul((float3x3)matView, oNormal);

                oTexcoord = Texcoord;
                oPosition = mul(matModelViewProject, oPosition);
            }

            void OpaquePS(
                in float3 iNormal : TEXCOORD0,
                in float2 iTexcoord : TEXCOORD1,
                out float4 fragData0 : SV_Target0,
                out float4 fragData1 : SV_Target1)
            {
                float3 albedo = quality.r > 0.0 ? texDiffuse.Sample(PointClamp, iTexcoord).rgb : diffuse.rgb;
                float3 normal = quality.g > 0.0 ? GetNormal(texNormal, iTexcoord, quality.g) : iNormal;

                fragData0 = StoreGBufferRT0(srgb2linear(albedo), specular);
                fragData1 = StoreGBufferRT1(normalize(normal.xyz), shininess);
            }
        ]]>
    </shader>
    <technique name="shadow">
        <pass name="p0">
            <state name="inputlayout" value="POS3F_W4F_B4UI"/>
            <state name="vertex" value="DepthVS"/>
            <state name="fragment" value="DepthPS"/>
            <state name="primitive" value="triangle" />
            <state name="stencilTest" value="true"/>
            <state name="stencilPass" value="replace"/>
            <state name="stencilTwoPass" value="replace"/>
        </pass>
    </technique>
    <technique name="opaque">
        <pass name="p0">
            <state name="inputlayout" value="POS3F_N3F_UV2F_W4F_B4UI"/>
            <state name="vertex" value="OpaqueVS"/>
            <state name="fragment" value="OpaquePS"/>
            <state name="primitive" value="triangle" />
            <state name="stencilTest" value="true"/>
            <state name="stencilPass" value="replace"/>
            <state name="stencilTwoPass" value="replace"/>
        </pass>
    </technique>
</effect>